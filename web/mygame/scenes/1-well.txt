*temp character
*temp char_select
*rand char_select 1 2
*if char_select = 1
	*set character "king"
*if char_select = 2
	*set character "troll"
*label start

*temp first_rejected
*set  first_rejected false
*temp second_rejected
*set  second_rejected false
*temp third_rejected
*set  third_rejected false

*label restart
*set  disposition 2

*temp the_brother
*set  the_brother current_brother & "_name"
*set  the_brother {the_brother}

*comment conversation-dependent inventory
*temp gold
*set  gold false
*temp cure
*set  cure false
*temp loyalty
*set  loyalty false

*comment character dependent dialogue and disposition changes
*temp payment_servitude
*set  payment_servitude character & "_payment_servitude"
*temp payment_money
*set  payment_money character & "_payment_money"
*temp payment_gold
*set  payment_gold character & "_payment_gold"
*temp payment_cure
*set  payment_cure character & "_payment_cure"
*temp payment_loyalty
*set  payment_loyalty character & "_payment_loyalty"

*temp he
*set  he character & "_he"
*set  he {he}
*temp his
*set  his character & "_his"
*set  his {his}
*temp name
*set  name character & "_name"
*set  name {name}
*temp desc
*set  desc character & "_desc"
*set  desc {desc}

*temp entrance1
*set  entrance1 character & "_entrance1"
*set  entrance1 {entrance1}
*temp entrance1_disp
*set  entrance1_disp character & "_entrance1_disp"
*temp entrance2
*set  entrance2 character & "_entrance2"
*set  entrance2 {entrance2}
*temp entrance2_disp
*set  entrance2_disp character & "_entrance2_disp"
*temp entrance3
*set  entrance3 character & "_entrance3"
*set  entrance3 {entrance3}
*temp entrance3_disp
*set  entrance3_disp character & "_entrance3_disp"

*temp response1
*set  response1 character & "_response1"
*set  response1 {response1}
*temp response1_disp
*set  response1_disp character & "_response1_disp"
*temp response2
*set  response2 character & "_response2"
*set  response2 {response2}
*temp response2_disp
*set  response2_disp character & "_response2_disp"
*temp response3
*set  response3 character & "_response3"
*set  response3 {response3}
*temp response3_disp
*set  response3_disp character & "_response3_disp"

*temp bargain
*set  bargain character & "_bargain"
*set  bargain {bargain}
*temp instareject
*set  instareject character & "_instareject"
*set  instareject {instareject}
*temp reject
*set  reject character & "_reject"
*set  reject {reject}

*temp ending1
*set  ending1 character & "_ending1"
*set  ending1 {ending1}
*temp ending2
*set  ending2 character & "_ending2"
*set  ending2 {ending2}
*temp ending3
*set  ending3 character & "_ending3"
*set  ending3 {ending3}

The brothers found the magic well in the woods where the old woman had said they would find it. $!{the_brother} stepped forward to retrieve the water, but a spirit appeared to stop him.

"I am the spirit that guards this well," the spirit said. "I am bound by duty to prevent all save the well's owner from drawing water from it. The well belongs to ${desc}."

*label action
*choice
	#"May I speak to ${name} and ask ${his} permission?" asked ${the_brother}.
		${entrance1}
		*set disposition + {entrance1_disp}
		*goto reason
	#"I do not have much, but if you let me draw some water, perhaps I could find a way to free you of your duty to ${name}." said ${the_brother}.
		${entrance2}
		*set disposition + {entrance2_disp}
		*goto reason
	#$!{the_brother} feigned a look of concern. "If none may draw from the well but its owner, then how is it that finch is carrying water away in a nutshell?"
		${entrance3}
		*set disposition + {entrance3_disp}
		*goto reason

*label reason
*choice
	#"Please," said ${the_brother}. "My mother is deathly ill, and I need this well water to cure her."
		*set loyalty true
		*set disposition + {response1_disp}
		${response1}
		*goto bargain
	#"This well water is one of several ingredients in a magical elixir," said ${the_brother}. "The making of which is known only to an acquaintance of mine. I am gathering the ingredients for her."
		*set cure true
		*set disposition + {response2_disp}
		${response2}
		*goto bargain
	#"I know of a spell that may turn this particular water to gold," said ${the_brother}. "If you let me have this water, I can make it worth your while."
		*set gold true
		*set disposition + {response3_disp}
		${response3}
		*goto bargain

*label bargain
*line_break
*line_break
*if disposition <= 0
	${instareject}
	*goto reset
*else
	${bargain}
	*goto loop_inner
	
*label loop
*line_break
*line_break
*if disposition <= 0
	${ending3}
	*goto end
*else
	*if disposition = 1
		$!{name} looked impatient. $!{the_brother} knew this was his last chance to convince ${name}.
		*line_break
		*line_break
	What will you offer?
	*label loop_inner
	*disable_reuse
	*choice
		#"I pledge to return once my errand is finished and serve you."
			*if {payment_servitude}
				${ending2}
				*if current_brother = "first"
					*set first_servitude true
					*goto end
				*elseif current_brother = "second"
					*set second_servitude true
					*goto end
				*elseif current_brother = "third"
					*set third_servitude true
					*goto end
				*else
					Error: ${current_brother} is not a valid brother.
					*goto end
			*else
				${reject}
				*set disposition -1
				*goto loop
		*if (inv_money) #"I will offer you what little money I have."
			*if {payment_money}
				*set inv_money false
				${ending1}
				*goto end
			*else
				${reject}
				*set disposition -1
				*goto loop
		*if (gold) #"I will give you some of the magic gold, once I have it."
			*if {payment_gold}
				${ending1}
				*goto end
			*else
				${reject}
				*set disposition -1
				*goto loop
		*if (cure) #"I will tell you the secret of the elixir."
			*if {payment_cure}
				${ending1}
				*goto end
			*else
				${reject}
				*set disposition -1
				*goto loop
		*if (loyalty) #"I offer you my family's eternal gratitude."
			*if {payment_loyalty}
				${ending1}
				*goto end
			*else
				${reject}
				*set disposition -1
				*goto loop

*label reset
*if current_brother = "first"
	*set first_rejected true
	*goto switch
*if current_brother = "second"
	*set second_rejected true
	*goto switch
*if current_brother = "third"
	*set third_rejected true
	*goto switch

*label switch
*label check1
*if ((first_servitude or first_rejected) and (second_servitude or second_rejected)) and (third_servitude or third_rejected)
	no more tries :(
	*ending
*if (current_brother = "first") and (first_servitude or first_rejected)
	*set current_brother "second"
	*goto check2
*label check2
*if (current_brother = "second") and (second_servitude or second_rejected)
	*set current_brother "third"
	*goto check3
*label check3
*if (current_brother = "third") and (third_servitude or third_rejected)
	*set current_brother "first"
	*goto check1
*page_break
*goto restart

*label end
*page_break
*set next_scene 2
*goto_scene selector